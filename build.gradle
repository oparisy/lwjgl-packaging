// Top level tasks: run, myCapsule, launch4j

buildscript {
	repositories {mavenCentral() }
	dependencies { classpath 'edu.sc.seis.gradle:launch4j:1.0.6' }
}

plugins {
	id 'com.stehno.natives' version '0.2.1'
	id 'us.kirchmeier.capsule' version '0.10.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'launch4j'

repositories {
	mavenCentral()
	// Required to get snashot version of Capsule
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

// Configure your application with the following properties
def mainClass = 'org.lwjgl.examples.spaceinvaders.Game'
def javaLevel = 1.7
def appArgs = '' // '-fullscreen'

// Use this to debug startup problems when using capsule + launch4j
def debugStartup = false

dependencies {
	compile 'org.lwjgl.lwjgl:lwjgl:2.9.2'
	compile 'org.lwjgl.lwjgl:lwjgl_util:2.9.2'
}

sourceCompatibility = targetCompatibility = javaLevel

natives {
	// Find native jars by name in dependencies
	jars = configurations.compile.files.collect{it.name}.findAll{it==~/.*-natives-.*/}
}

// Unpacked native libraries folders
def nativePaths = ['windows', 'linux', 'osx'].collect{'build/natives/' + it}

run {
	mainClassName = mainClass
	systemProperty 'java.library.path', files(nativePaths).asPath
	args appArgs
}

// With this, "gradle clean run" will work
tasks.run.dependsOn(unpackNatives)

// Solve Capsule issue #46
configurations { myCapsule }
dependencies { myCapsule 'co.paralleluniverse:capsule:0.10.1-SNAPSHOT' }

def minJavaVersion = javaLevel + '.0'
def capsuleJarName = project.name + "-capsule.jar"

// See http://blog.paralleluniverse.co/2014/05/08/modern-java-pt2/
task myCapsule(type: FatCapsule, dependsOn: [jar, unpackNatives]) {
	capsuleConfiguration = configurations.myCapsule
	archiveName = capsuleJarName

	// Embed application jar and dependencies
	applicationSource jar
	applicationClass mainClassName
	embedConfiguration = configurations.runtime { exclude '*-natives-*'}

	// Copy native libraries to capsule root
	from (nativePaths)

	// We just need the Capsule class (no runtime download)
	capsuleFilter = { include 'Capsule.class' }

	manifest {
		attributes(
				'Main-Class'  : 'Capsule',
				'Application-Class' : mainClassName,
				'Min-Java-Version' : minJavaVersion,
				'JVM-Args' : run.jvmArgs.join(' '), // copy JVM args from the run task
				'Args' : appArgs
				)
		if (debugStartup) {
			attributes 'System-Properties': 'capsule.log=verbose'
		}
	}
}

launch4j {
	mainClassName = 'Capsule'
	jar = file('build/libs/' + capsuleJarName)
	jreMinVersion = minJavaVersion
	outfile = project.name + '.exe'
	if (debugStartup) {
		headerType = 'console'
	}
}

// Ensure that executable is created last (adding a launch4j => myCapsule dependency does not suffice)
tasks.createExe.dependsOn(tasks.myCapsule)
